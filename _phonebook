#compdef phonebook

# Copyright (c) 2018, CERN for the benefit of the LHCb collaboration
# Author: Paul Seyfert <pseyfert@cern.ch>
#
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#     * Redistributions of source code must retain the above copyright
#       notice, this list of conditions and the following disclaimer.
#     * Redistributions in binary form must reproduce the above copyright
#       notice, this list of conditions and the following disclaimer in the
#       documentation and/or other materials provided with the distribution.
#     * Neither the name of the <organization> nor the
#       names of its contributors may be used to endorse or promote products
#       derived from this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
# ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
# WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL <COPYRIGHT HOLDER> BE LIABLE FOR ANY
# DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
# (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
# LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
# ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
# SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

# In applying this licence, CERN does not waive the privileges and immunities
# granted to it by virtue of its status as an Intergovernmental Organization
# or submit itself to any jurisdiction.

_order() {
  local vas;
  vals=(
  'surname'
  'firstname'
  'login'
  'department'
  'group'
  'cerngroup'
  'cernsection'
  'displayname'
  )
  _values 'field for sorting' ${vals[@]}
}

_terse() {
  local vas;
  vals=(
  'surname'
  'firstname'
  'phone'
  'otherphone'
  'mobile'
  'fax'
  'office'
  'department'
  'group'
  'pobox'
  'email'
  'displayname'
  'organization'
  'ccid'
  'login'
  'homedir'
  'last'
  'uid'
  'gid'
  'uac:user account control for active directory'
  'type:primary, secondary, or service account'
  'emailnick'
  'company'
  'shell'
  'secid:secure identities mapped to account'
  )
  _describe 'printout fields' vals
}

local options
options=(
"(- 1)--help[show help]"
"(--help)--surname[search by surname]:surname:"
"(--help)--firstname[search by firstname]:first name:"
"(--help)--building[search by building]:building:"
"(--help)--office[search by office]:floor-office, office, or floor:"
"(--help)--email[search by email address]:email address:"
"(--help)--login[search for a cern login]:login:"
"(--help)--department[search for people in a department]:department:"
"(--help)--group[search for group members]:group:"
"(--help)--section[search for people in a section]:section:"
"(--help)--phone[search for people with a matching phone number]:phone number:"
"(--help)--mobile[search for people with a certain mobile phone number]:mobile phone number:"
"(--help)--pobox[search for people by post box]:post box:"
"(--help)--ccid[search for a computer center ID]:computer center ID:"
"(--help)--uid[search for a computer user ID match]:computer user ID:"
"(--help)--gid[search for a computer group ID match]:computer group ID:"
"(--help)--homedir[search for users with matching home dir]:home dir (escape LDAP wild cards)"
"(--help)--externals[search only for external lightweight accounts]"
"(--help)--yellowpages[search for services instead of users]"
"(--help --terse)--all[show all information]"
"(--help)--orderby[define search order]:order:_order"
"(--help --all)*--terse[specify output fields]:print fields:_terse"
)

_arguments "$options[@]"

